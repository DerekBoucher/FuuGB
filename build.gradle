/*
    Gradle BuildScript for FuuGBemu
    C++ Gameboy (DMG) Emulator
    Author: Derek Boucher
*/

/*
    Packages
*/
import org.gradle.internal.os.OperatingSystem


/*
    Required Plugins For FuuGBemu
*/
plugins {
    id 'cpp'
    id 'objective-cpp'
    id 'cpp-unit-test'
}

/*
    Global Variables
 */
def sdl2Lib
def sdl2Include
def sdl2Dll
def sdl2ImageInclude
def sdl2ImageDll
def sdl2ImageLib
def sdl2TtfInclude
def sdl2TtfLib
def sdl2TtfDll



/*
    Determine Platform Specific Library Paths
 */
 sdl2Include = 'lib/SDL2/include'
 sdl2ImageInclude = 'lib/SDL2_Image/include'
 sdl2TtfInclude = 'lib/SDL2_ttf/include'
if (OperatingSystem.current().isWindows()) {
    sdl2Lib = file(OperatingSystem.current().getStaticLibraryName('lib/SDL2/Windows/x64/SDL2'))
    sdl2Dll = file(OperatingSystem.current().getSharedLibraryName('lib/SDL2/Windows/x64/SDL2'))
    sdl2ImageLib = file(OperatingSystem.current().getStaticLibraryName('lib/SDL2_Image/Windows/SDL2_image'))
    sdl2ImageDll = file(OperatingSystem.current().getSharedLibraryName('lib/SDL2_Image/Windows/SDL2_image'))
    sdl2TtfLib = file(OperatingSystem.current().getStaticLibraryName('lib/SDL2_ttf/Windows/SDL2_ttf'))
    sdl2TtfDll = file(OperatingSystem.current().getSharedLibraryName('lib/SDL2_ttf/Windows/SDL2_ttf'))
}
if (OperatingSystem.current().isMacOsX()) {
    sdl2Lib = file(OperatingSystem.current().getSharedLibraryName('lib/SDL2/MacOsX/SDL2'))
}

/*
    Model Block:
        This block defines the platform, architecture, compiler and linker arguments,
        the platform specific source sets and binary types.
*/
model {
    platforms {
        windows_x64 {
            architecture "x86_64"
            operatingSystem "windows"
        }
        macosx_x64 {
            architecture "x86_64"
            operatingSystem "macosx"
        }
    }
    repositories {
        libs(PrebuiltLibraries) {
            SDL2 {
                headers.srcDirs sdl2Include
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = sdl2Lib
                }
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = sdl2Lib
                }
            }
            SDL2Image {
                headers.srcDirs sdl2ImageInclude
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = sdl2ImageLib
                }
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = sdl2ImageLib
                }
            }
            SDL2Ttf {
                headers.srcDirs sdl2TtfInclude
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = sdl2TtfLib
                }
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = sdl2TtfLib
                }
            }
        }
    }
    components {
        FuuGBemu(NativeExecutableSpec) {
            sources {
                if (OperatingSystem.current().isMacOsX()) {
                    targetPlatform "macosx_x64"
                    objcpp {
                        source {
                            srcDirs "src/main/cpp", "src/main/MacOSX"
                            include "**/*.mm", "**/*.m", "**/*.cpp"
                        }
                        exportedHeaders {
                            srcDirs "src/main/headers"
                        }
                        lib library: "SDL2", linkage: "shared"
                    }
                }
                if (OperatingSystem.current().isWindows()) {
                    targetPlatform "windows_x64"
                    cpp {
                        source {
                            srcDirs "src/main/cpp", "src/main/Windows", "vendor/guisan/src"
                            include "**/*.cpp"
                        }
                        exportedHeaders {
                            srcDirs "src/main/headers", "vendor/guisan/include"
                            include "**/*.h", "**/*.hpp"
                        }
                        lib library: "SDL2", linkage: "shared"
                        lib library: "SDL2Image", linkage: "shared"
                        lib library: "SDL2Ttf", linkage: "shared"
                    }
                }
            }
            binaries.all {
                if(OperatingSystem.current().isMacOsX()) {
                    objcppCompiler.args "-DFUUGB_SYSTEM_MACOS",
                                        "-DFUUGB_DEBUG",
                                        "-g",
                                        "-Wall",
                                        "-std=c++11",
                                        "-stdlib=libc++",
                                        "-pthread",
                                        "-O3"

                    linker.args         "-framework",
                                        "Cocoa",
                                        "-framework",
                                        "AppKit"
                }
                if(OperatingSystem.current().isWindows()) {
                    cppCompiler.args    "/FS",
                                        "/Zi",
                                        "/MTd",
                                        "/MP",
                                        "/DFUUGB_SYSTEM_WINDOWS",
                                        "/DUNICODE",
                                        "/D_UNICODE",
                                        "/DFUUGB_DEBUG",
                                        "/EHsc",
                                        "/O2"

                    linker.args         "/DEBUG",
                                        "comdlg32.lib",
                                        "user32.lib",
                                        "opengl32.lib"
                }
            }
            build {
                doLast {
                    copy {
                        from 'lib/bootrom/DMG_ROM.bin'
                        into 'build/exe/fuuGBemu'
                    }
                    if (OperatingSystem.current().isMacOsX()) {
                        copy {
                            from 'lib/SDL2/MacOsX/libSDL2.dylib'
                            into 'build/exe/fuuGBemu'
                        }
                    }
                    if (OperatingSystem.current().isWindows()) {
                        copy {
                            from 'lib/SDL2/Windows/x64/SDL2.dll'
                            into 'build/exe/fuuGBemu'
                        }
                        copy {
                            from 'lib/SDL2_Image/Windows'
                            into 'build/exe/fuuGBemu'
                        }
                        copy {
                            from 'lib/SDL2_ttf/Windows'
                            into 'build/exe/fuuGBemu'
                        }
                        copy {
                            from 'vendor/guisan/examples/fixedfont.bmp'
                            into 'build/exe/fuuGBemu'
                        }
                    }
                }
            }
            unitTest {
                source {
                    from "src/main/cpp"
                    from "src/test/cpp"
                    from "src/test"
                }
                privateHeaders {
                    from "src/main/headers"
                    from "src/test/headers"
                }
            }
            compileTestCpp {
                includes.from(sdl2Include)
                compilerArgs.addAll toolChain.map { toolChain ->
                    if (toolChain in VisualCpp) {
                        return [
                            '/MT',
                            '/DFUUGB_SYSTEM_WINDOWS',
                            '/DUNICODE',
                            '/DFUUGB_UNIT_TEST'
                        ]
                    } else {
                        return [
                            '-x',
                            'objective-c++',
                            '-std=c++11',
                            '-Wall',
                            '-O3',
                            '-DFUUGB_SYSTEM_MACOS',
                            '-DFUUGB_UNIT_TEST'
                        ]
                    }
                }
            }
            linkTest {
                libs.from(sdl2Lib)
                linkerArgs.addAll toolChain.map { toolChain ->
                    if (toolChain in VisualCpp) {
                        return [
                            'comdlg32.lib',
                            'user32.lib'
                        ]
                    } else {
                        return [
                            '-framework',
                            'Cocoa',
                            '-framework',
                            'AppKit'
                        ]
                    }
                }
                doLast {
                    if (OperatingSystem.current().isMacOsX()) {
                        copy {
                            from 'lib/SDL2/MacOsX/libSDL2.dylib'
                            into 'build/exe/test'
                        }
                    }
                    if (OperatingSystem.current().isWindows()) {
                        copy {
                            from 'lib/SDL2/Windows/x64/SDL2.dll'
                            into 'build/exe/test'
                        }
                    }
                }
            }
            installTest {
                doLast {
                    if (OperatingSystem.current().isMacOsX()) {
                        copy {
                            from 'lib/SDL2/MacOsX/libSDL2.dylib'
                            into 'build/install/test/lib'
                        }
                    }
                    if (OperatingSystem.current().isWindows()) {
                        copy {
                            from 'lib/SDL2/Windows/x64/SDL2.dll'
                            into 'build/install/test/lib'
                        }
                    } 
                }
            }
        }
    }
}